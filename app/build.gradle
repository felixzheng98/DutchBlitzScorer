plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp' version '1.8.10-1.0.9'
    // Can remove kapt in favor of KSP after https://github.com/google/dagger/issues/2349
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'androidx.navigation.safeargs'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.zhengineer.dutchblitzscorer'
    compileSdk 33

    defaultConfig {
        applicationId "com.zhengineer.dutchblitzscorer"
        minSdk 27
        targetSdk 33
        versionCode 4
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "version_name", variant.versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        dev_release {
            debuggable true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            versionNameSuffix '-dev'
        }
        debug {
            versionNameSuffix '-dbg'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlin {
        jvmToolchain {
            jvmToolchain(11)
        }
    }
    kotlinOptions {
        jvmTarget = '11'
    }
    lintOptions {
        // Disabling due to bug with linter, believing SettingsActivityImpl does not extend
        // android.app.Activity
        disable "Instantiatable"
    }
    buildFeatures {
        viewBinding true
    }
    kapt {
        correctErrorTypes true
    }
    ksp {
        arg('room.schemaLocation', "$projectDir/schemas")
    }
}

dependencies {
    implementation project(path: ':commons')

    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.fragment:fragment-ktx:1.6.0-alpha04'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'com.google.android.material:material:1.9.0-alpha01'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.recyclerview:recyclerview-selection:1.2.0-alpha01'

    // Nav
    implementation 'androidx.hilt:hilt-navigation-fragment:1.0.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'

    // Hilt
    implementation 'com.google.dagger:hilt-android:2.45'
    kapt 'com.google.dagger:hilt-compiler:2.45'

    // preferences
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'com.takisoft.preferencex:preferencex:1.1.0'

    // database
    def room_version = '2.5.0'
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    ksp "androidx.room:room-compiler:$room_version"

    // firebase - https://firebase.google.com/support/release-notes/android
    implementation platform('com.google.firebase:firebase-bom:31.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}